APPROACH: $keep_gate FROM $staging_area

APPROACH: $keep_gate FROM $keep_bedroom
DESCRIPTION:
[[IF !c.playerHasVisited('keep_gate')]]I open a massive door and walk through it into a corridor that looks a lot less ornamented than anything in the Keep. I turn around to behold the entrance to this aristocratic home.[[ENDIF]]

// ---
ROOM: $keep_gate

FLAGS: $IDLE

POS: 20, 81

FIRST_MAP_NAME: The Keep
FIRST_HINT: This part of the Pyramid seems to have been rebuilt at some point into an aristocratic residence. This is the floor where I saw the lady with the katana.

MAP_NAME: The Keep's Gate
HINT: The entrance to the abandoned aristocratic residence within the Pyramid.

FIRST_DESCRIPTION:
The plain, ancient geometry of the Pyramid's hallways is disrupted by a massive gate. The gate is made from dark, richly ornamented redwood, and a rich wet scent fills the space in front of it.

This is a lord's home, except it doesn't stand on top of a hill or next to a lake. Instead, it was fashioned from some of the more well-preserved rooms in the ancient building.

DESCRIPTION: $NONE


// ---
ACTION: $examine_gate
COMMAND: Gate >> Examine
FOR_LOCATION: $keep_gate

COMPLETE_SUCCESS_DESCRIPTION:
[[RULESET]]
[[RULE]]
  c.hasHappened('destroy_gate_with_axe')
[[THEN]]
  The strict simplicity of the ancient building gives way to Nortonian redwood panels. A fine piece of carpentry. Obviously expensive to build. A shame that I had to destroy it.
[[ENDRULE]]
[[RULE]]
  $DEFAULT
[[THEN]]
  The strict simplicity of the ancient building gives way to Nortonian redwood panels. A fine piece of carpentry. Obviously expensive to build.
[[ENDRULE]]
[[ENDRULESET]]

A big warning sign on the wall says "HAUNTED." Below the paint, there is an older, fainter sign. It says "eat the rich."


// ---
ACTION: $attempt_open_gate
COMMAND: Gate >> Open
FOR_LOCATION: $keep_gate
PREREQUISITES:
!c.hasHappened(evKeepDestroyedGate)
&&
!c.knows(KeepGateFacts.keepGateUnlock)
&&
!c.playerHasVisited("keep_bedroom")

COMPLETE_SUCCESS_DESCRIPTION:
I attempt to open the gate but it's closed tight. Maybe there's a trick to unlocking it using some of the intricate woodwork, but my random mashing of various ornaments does nothing.

I could also bring it down using an axe. It's wood, after all.


// ---
ACTION: $open_gate_unlock
COMMAND: Gate >> Open
FOR_LOCATION: $keep_gate
PREREQUISITES:
!c.hasHappened(evKeepDestroyedGate)
&&
c.knows(KeepGateFacts.keepGateUnlock)
&&
!c.playerHasVisited("keep_bedroom")

INK:
I start following the steps I learned. It is quite easy to find the decorative parts that I need to push, turn, or pull: they seem more polished than the rest of the gate. They are polished by fingers performing the ritual over and over again, for decades.

Finally, I am at the very end of the "algorithm." All that's left is to turn the crest in the middle of the gate.

* 7 times

  I am pretty sure it was seven times, a lucky number. When I'm done with the seventh revolution, I step back. The gate stands still for a while, then something clicks inside.

  I am showered by countless little darts, shooting diagonally from the ceiling. A curtain of blood covers my eyes, and the view of the gate fades to eternal black.

  [[CODE]]
  // Kill player.
  w.updateActorById(playerId, (b) => b.hitpoints = 0);
  w.recordCustom(CustomEvent.actorDeath, actor: c.player);
  [[ENDCODE]]

* 8 times

  Eight times, like the Eight Gods. When I've finished the eighth revolution, I step back. The gate stands still for a while, then something clicks inside.
  The wings of the gate open and reveal a beautifully decorated passage into the Keep.

  [[CODE]]
  w.recordCustom(evKeepUnlockedGate);
  [[ENDCODE]]

* 9 times

  I am pretty sure it was nine times, nine like cat lives. When I'm done with the ninth revolution, I step back. The gate stands still for a while, then something clicks inside.

  I am showered by countless little darts, shooting diagonally from the ceiling. A curtain of blood covers my eyes, and the view of the gate fades to eternal black.

  [[CODE]]
  // Kill player.
  w.updateActorById(playerId, (b) => b.hitpoints = 0);
  w.recordCustom(CustomEvent.actorDeath, actor: c.player);
  [[ENDCODE]]


// ---
ACTION: $destroy_gate_with_axe
COMMAND: Gate >> Destroy
FOR_LOCATION: $keep_gate
PREREQUISITES:
c.player.inventory.hasWeapon(WeaponType.axe)
&&
!c.hasHappened(evKeepUnlockedGate)
&&
!c.playerHasVisited("keep_bedroom")

COMPLETE_SUCCESS_DESCRIPTION:
[[CODE]]
  final axe = c.player.inventory.findBestWeapon(type: WeaponType.axe);
  a.report(s, "<subject> chop<s> down the gate with <objectNounWithAdjective>", object: axe);

  w.recordCustom(evKeepDestroyedGate);
[[ENDCODE]]

This reveals a beautifully decorated passage into the Keep.


NOTES:
// A patrol might come by at points.

// Vignette

The Keep

A part of the pyramid previously inhabited by aristocrats. These days, it's deserted. People are afraid of the ghost that dwells there. And lately, some people saw goblins.

There were some tomb raider there, snatching stuff. But the last party that was there came back decimated and insane.

The Keep is full of decrepit beauty. Rich people with taste lived there for fifty years.

There is the dining room, with a table of food that is as if people left in a hurry. Except it's clear that happened a decade ago. The dining room has a great overview of the surroundings. Lots of light. This is where Last Hope stands, watching.

There are the lord's quarters, which still has some coin. You can "rummage through" stuff. Maybe you get some information about the "house improvements" in the pyramid. There is some sense that money is a form of legacy. And money begets money, so it's a self perpetuating legacy. But money and convenience, in itself, is vanity. You have to use the money to have actual legacy.

There are also servants quarters, with evidence of goblins.
