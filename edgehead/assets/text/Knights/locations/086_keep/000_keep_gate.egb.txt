APPROACH: $keep_gate FROM $staging_area
APPROACH: $keep_gate FROM $keep_bedroom

// ---
ROOM: $keep_gate

FLAGS: $IDLE

POS: 20, 81

FIRST_MAP_NAME: The Keep
FIRST_HINT: This part of the Pyramid seems to have been carved into an artistocratic residence. This is the floor where I saw the lady with the katana.

MAP_NAME: The Keep's Gate
HINT: The entrance to the abandoned aristocratic mansion embedded into the Pyramid.

FIRST_DESCRIPTION:
The plain, ancient geometry of the Pyramid's hallways is suddenly disrupted by a massive gate. The gate is from dark, richly ornamented redwood.

This is a lord's house, except it doesn't stand on top of a hill or next to a lake. Instead, it is embedded in the ancient building.

DESCRIPTION: $NONE


// ---
ACTION: $examine_gate
COMMAND: gate >> examine
FOR_LOCATION: $keep_gate

COMPLETE_SUCCESS_DESCRIPTION:
// TODO: by the time we examine, the gate could have been destroyed
The strict simplicity of the ancient building gives way to nortonian redwood panels. A fine piece of carpentry. Obviously expensive to build.

// Include some hints that Ada might know about it.

A big warning sign on the wall says "Haunted." Below the paint, an older, fainter sign says "Eat the rich".


// ---
ACTION: $attempt_open_gate
COMMAND: gate >> open
FOR_LOCATION: $keep_gate
PREREQUISITES:
!c.hasHappened(evKeepDestroyedGate)
&&
!c.knows(KeepGateFacts.keepGateUnlock)

COMPLETE_SUCCESS_DESCRIPTION:
I attempt to open the gate but it's closed tight. Maybe there's a trick to unlock it using some of the intricate woodwork, but my random mashing of various ornaments does nothing.

I could also bring it down using an axe. It's wood, after all.


// ---
ACTION: $open_gate_unlock
COMMAND: gate >> open
FOR_LOCATION: $keep_gate
PREREQUISITES:
!c.hasHappened(evKeepDestroyedGate)
&&
c.knows(KeepGateFacts.keepGateUnlock)

COMPLETE_SUCCESS_DESCRIPTION:
I unlock the gate using the steps I learned. This reveals a beatifully decorated passage into the Keep.

[[CODE]]
w.recordCustom(evKeepUnlockedGate);
[[ENDCODE]]

// ---
ACTION: $destroy_gate_with_axe
COMMAND: gate >> destroy
FOR_LOCATION: $keep_gate
PREREQUISITES:
c.player.inventory.hasWeapon(WeaponType.axe)
&&
!c.hasHappened(evKeepUnlockedGate)

COMPLETE_SUCCESS_DESCRIPTION:
[[CODE]]
final axe = c.player.inventory.findBestWeapon(type: WeaponType.axe);
a.report(s, "<subject> chop<s> down the gate with <objectNounWithAdjective>", object: axe);

w.recordCustom(evKeepDestroyedGate);
[[ENDCODE]]

This reveals a beatifully decorated passage into the Keep.


NOTES:
// TODO: the three different ways to get past the gate: Talk to someone and learn the access path; Use stamina to move open a nailed door, or use an axe.; Descend from way above

// A patrol might come by at points.

// Vignette

The Keep

A part of the pyramid previously inhabited by aristocrats. These days, it's deserted. People are afraid of the ghost that dwells there. And lately, some people saw goblins.

There were some tomb raider there, snatching stuff. But the last party that was there came back decimated and insane.

The Keep is full of decrepit beauty. Rich people with taste lived there for fifty years.

There is the dining room, with a table of food that is as if people left in a hurry. Except it's clear that happened a decade ago. The dining room has a great overview of the surroundings. Lots of light. This is where Last Hope stands, watching.

There are the lord's quarters, which still has some coin. You can "rummage through" stuff. Maybe you get some information about the "house improvements" in the pyramid. There is some sense that money is a form of legacy. And money begets money, so it's a self perpetuating legacy. But money and convenience, in itself, is vanity. You have to use the money to have actual legacy.

There are also servants quarters, with evidence of goblins.


